#!/usr/bin/perl
# tlp-pcilist - list pci devices with runtime pm mode and device class
#
# Copyright (c) 2014 Thomas Koch <linrunner at gmx.net>
# This software is licensed under the GPL v2 or later.


package tlp_pcilist;
use strict;
use warnings;
use File::Basename;

# get sysfs value, return empty string if nonexistent or not readable
sub catsysf {
    my $sysval = "";
    if (open SYSF, "$_[0]") {
        chomp ($sysval = <SYSF>);
        close SYSF;
    }
    return $sysval;
}

# output device list with runtime pm mode and device class
foreach (`lspci -m`) {
    # parse lspci output: get short PCI(e) id and long description of device
    my ($dev, $classdesc) = /(\S+) \"(.+?)\"/;
    # join device path
    my $devp = "/sys/bus/pci/devices/0000:$dev";
    # control file for Runtime PM
    my $devc = "$devp/power/control";

    if (-f $devc) { # control file exists

        # get Runtime PM mode
        my $pmode = catsysf ("$devc");
        # get device class
        my $class = catsysf ("$devp/class");

        # read path to driver from softlink
        my $driver = readlink ("$devp/driver");
        if ( (defined $driver) and ($driver ne "") ) {
            # basename(path) = driver name
            $driver = basename($driver);
        } else {
            # no driver assigned
            $driver = "no driver";
        }

        # output device data
        printf "%s/power/control = %-4s (%s, %s, %s)\n", $devp, $pmode, $class, $classdesc, $driver;
    }
}
